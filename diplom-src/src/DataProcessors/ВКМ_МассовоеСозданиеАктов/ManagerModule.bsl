#Область СлужебныеПроцедурыИФункции


Функция СозданиеРеализацийЗаПериод(ДатаНачала, ДатаОкончания) Экспорт	
	
	МассивПериодов = Новый Массив;
	ТекущийПериод = ДатаНачала;
	Пока ТекущийПериод < ДатаОкончания Цикл
		НовыйПериод = Новый СтандартныйПериод(НачалоМесяца(ТекущийПериод), КонецМесяца(ТекущийПериод));
		МассивПериодов.Добавить(НовыйПериод);
		ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 1);
	КонецЦикла; 
	
	ТабРеализаций = Новый ТаблицаЗначений;
	ТабРеализаций.Колонки.Добавить("Договор");
	ТабРеализаций.Колонки.Добавить("ДокументРеализации");	
	
	Для Каждого ТекПериод Из МассивПериодов Цикл		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДоговорыКонтрагентов.Ссылка КАК Договор
			|ПОМЕСТИТЬ Договоры
			|ИЗ
			|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
			|ГДЕ
			|	ДоговорыКонтрагентов.ВКМ_ДатаНачала >= &ДатаНачала
			|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончания <= &ДатаОкончания
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РеализацияТоваровУслуг.Ссылка КАК Реализация
			|ПОМЕСТИТЬ Реализации
			|ИЗ
			|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
			|ГДЕ
			|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Договоры.Договор.Организация КАК Организация,
			|	Договоры.Договор.Владелец КАК Контрагент,
			|	Договоры.Договор КАК Договор,
			|	Реализации.Реализация КАК Реализация
			|ИЗ
			|	Договоры КАК Договоры
			|		ЛЕВОЕ СОЕДИНЕНИЕ Реализации КАК Реализации
			|		ПО Договоры.Договор = Реализации.Реализация.Договор
			|ГДЕ
			|	Реализации.Реализация ЕСТЬ NULL";
	
		Запрос.УстановитьПараметр("ДатаНачала", ТекПериод.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", ТекПериод.ДатаОкончания);
	
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда  
			Продолжить;
		КонецЕсли;
		
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			
			НовыйДокументРеализации = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			НовыйДокументРеализации.Дата = ТекПериод.ДатаОкончания;
			НовыйДокументРеализации.Организация   = Выборка.Организация;
			НовыйДокументРеализации.Контрагент    = Выборка.Контрагент;
			НовыйДокументРеализации.Договор       = Выборка.Договор;
			НовыйДокументРеализации.Ответственный = ПараметрыСеанса.ТекущийПользователь;
			
			
			НовыйДокументРеализации.ВКМ_ВыполнитьАвтозаполнение();
			Если НовыйДокументРеализации.Услуги.Количество()>0 Тогда 
				
				Попытка	
					НовыйДокументРеализации.Записать(РежимЗаписиДокумента.Проведение);
					
					НовСтрока = ТабРеализаций.Добавить();
					НовСтрока.Договор = Выборка.Договор;
					НовСтрока.ДокументРеализации = НовыйДокументРеализации.Ссылка;
				Исключение
					
					ЗаписьЖурналаРегистрации("ОшибкаСозданияДокументовРТУ.Ошибка", УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ВызватьИсключение
					
				КонецПопытки;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;	
	
	Возврат ТабРеализаций;
	
КонецФункции

#КонецОбласти

